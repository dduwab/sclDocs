# Cloning Sprites
Cloning is one of the most powerful features of SCL. When you clone a sprite, the new sprite gets all the properties and alterations of the original sprite. You can clone a sprite as it was originally defined. A clone can be assigned a name using **as {cloneName}**, but this is optional. Immediately after you clone a sprite you can refer to the clone to make updates by using **_clone** instead of a sprite name, as in ```update sprite _clone where x=7``` 

```
create sprite from star.png as Star
  where x=30 y=30 angle=45 center=auto
  having alt=(sub create rotation end)
end
create routine as Start
  launch Star
  clone from Star using original
  update sprite _clone where image=heart.png
  update sprite _clone where x=130 and y=130
  clone from Star using original as Square
  update sprite _clone where image=square.png
  update sprite _clone where x=230 and y=230

  update sprite Square where size=2
end
```
> [!NOTE]
> Try out code samples at https://canvaslanguage.com/studio/github-examples.
> Paste the code samples into the box under that "Advanced" tab,
> then push the "Rec" button to view the animation.

You can assign a name to a clone using **as**: ```clone from Empty as Ducky using original```

If you name a clone then you can reference it like any other sprite. You should not assign a name if the same clone statement will be used repeatedly. 

Please note the **_clone** keyword. _clone always refers back to the most recently created clone. The value of _clone can be used until a new clone is created - even in other routines. 

## 100s
Making multiple sprites automatically requires a simple construction where you create a routine to make a clone and repeat that routine as needed.

```
create sprite from star.png as Star
  where x=30 y=130 center=auto
  having alt=(sub create rotation end)
end
create routine as Start
  run routine LaunchStar where repeat=9
end
create routine as LaunchStar
  clone from Star using original
  update sprite _clone where x=_repeat*50
end
```
As you can see, the built-in variable **_repeat** indicates which iteration you are on. The first value is 1 (ie: 1-based indexing).

```
create sprite from star.png as Star
  where x=30 y=130 center=auto
  having alt=(sub create rotation end)
end
create routine as Start
  run routine LaunchStar where repeat=500
end
create routine as LaunchStar
  clone from Star using original
  update sprite _clone where x=random(50,450) and y=random(50,450) and size=random(1,10)/10
end
```
Just a reminder, you must use **and** when joining a list of property changes in an **update** command.

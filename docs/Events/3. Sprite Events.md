# Sprite Events

Individual event handlers can be assigned to individual sprites. This is done using parameters in the **where** clause.

```
create sprite from square.png as S1
  where click=OnClick 
end
```

## User Events
The events below are similar to canvas events, but they are only triggered when the action is taken on a portion of the sprite that is not 100% transparent. 
- **click**
- **mouseup**
- **mousedown**
- **mousein**
- **mouseout**
- **mousemove**
- **touchstart** 
- **touchend**
- **touchdrag**
- **swipeup**
- **swiperight**
- **swipedown**
- **swipeleft**

## Collision Detection
The two event below are for collision detection, that is, handling events for when sprites collide.
- **where**
  - **beenhit** calls the event handler when any portion of the sprite that is not 100% transparent is "hit" by the center point of another sprite where the hitting sprite has this sprite specified as a target.
  - **hashit** calls event handler when the center point of this sprite hits a portion of a target sprite that is not 100% transparent
- **having**
  - **target** indicates a single sprite to collide with
  - **targets** indicates the name of a group. Sprites in that group will all be targets for collision. Specify groups using the **into** clause of a sprite declaration
```
create sprite from STAR as Star into SpaceStuff end
create sprite from HEART as Moon into SpaceStuff end
create sprite from SQUARE as Comet
  where hashit=CometHitSomething
  having targets=SpaceStuff
end
```

  
> [!NOTE]
> Try out code samples at https://canvaslanguage.com/studio/github-examples.
> Paste the code samples into the box under that "Advanced" tab,
> then push the "Rec" button to view the animation.
```
var STAR="star.png"
var TRIANGLE="triangle.png"

create sprite from STAR as Star
  where center=auto where x=350 y=250 beenhit=SBeen
end
create sprite from TRIANGLE as Triangle
  where center=auto where x=150 y=250 hashit=THas
  having target=Star alt=Move
end
create routine as SBeen
  update sprite _sprite where size=0.25
end
create routine as THas
  update sprite _sprite where alpha=0.25
  remove from _sprite where alt=Move
end
create routine as Start
  launch Star
  launch Triangle
end
create slide as Move
  where speed=100 direction=0
end
```
The keen-eyed observers will notice that the sprites overlap before the event is triggered. This is because of the center being the triggering mechanism. This is done for efficiency purposes in checking for the collision. It's not perfect, but it's quick and will do for now. One trick to improve this is to simply set your "center" off center, for example at the tip of an arrow.

### Special Collision Detection arguments
The event handlers of collision detection define 2 special arguments automatically depending on if the event is a **beenhit** event or a **hashit** event. Simply put, those special arguments are the two sprites involved in the collision. For **beenhit** events, the first argument is the sprite that has been hit, for **hashit**, the arguments are the other way around.
```
create routine as BeenHitted
  update sprite arg.1 where size=0.25
  update sprite arg.2 where alpha=0.25
end
create routine as HasHitted
  update sprite arg.1 where alpha=0.25
  update sprite arg.2 where size=0.25
end
```
In the example above, both event handles have the same effect on both sprites because they set the appropriate sprites according to the argument order: The sprite that is hit is shrunk and the sprite doing the hitting has its alpha changed.

## Animation Events
The following event handlers are animation related. They are called for the sprite at the given instance, and each will have the calling sprite accessible through the _sprite variable.
- **enterframe** called when a new frame in the animation is entered before anything is done
- **penultimate** calls event handler after the sprites of the frame have been processed, but before the **var** conditions have been checked.  
- **exitframe** calls event handler after all sprite processing has been completed and after the **penultimate** event has been called.

Please note that when processing sprites using the three event handlers above, there is no guarantee as to the order the handlers will be called in relation to the sprites reacting to them. In fact, that was one of the reasons for implementing the **penultimate** event handler. It gives sprites a chance to react to one another.

```
create sprite from STAR as Star
  where enterframe=EFStar penultimate=PenStar exitframe=EFStar
end
```


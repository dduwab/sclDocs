# Child Sprites
SCL allows you to create unlimited parent/child sprite relationships. A child sprite is displayed in a parent sprite and animates the same way as its parent. But beyond this, a child sprite can have its own alterations that do not affect its parent sprite.

Child sprites are normally specified in the definition of the parent sprite.

```create sprite as face having children=(eyes,nose,mouth) end```

All child sprites must be defined in the normal way, however the x/y position of the child sprite is relative to the center point of the parent sprite.
**IMPORTANT**: more than one child sprite must be listed inside parenthesis, but a single child MUST NOT be in parenthesis.

Another useful way to specify a parent sprite is in routines using ```set parent={spriteName}```. That instruction forces all sprites launched after the command to be children of the sprite given. To cancel this command use ```set parent=_none```. Animations that want to transition in an entire scene may find this very useful.

You can also add and remove child sprites in routines using the **remove** and **insert** instructions. See how in the reference section.

> [!NOTE]
> Try out code samples at https://canvaslanguage.com/studio/github-examples.
> Paste the code samples into the box under that "Advanced" tab,
> then push the "Rec" button to view the animation.

```
create sprite from triangle.png as Triangle
  where center=auto x=250 y=250
  having children=(Square,Heart)
  alt=(sub create rotation end)
end
create sprite from heart.png as Heart
  where center=auto x=0 y=-30
end
create sprite from square.png as Square
  where center=auto x=60 y=0
end
create routine as Start
  launch Triangle
end
```

## Layers

Child sprites are also very useful for creating layers. You can create a sprite that doesn't specify and image, the use **set parent** to arbitrarily launch new sprites into it.

This example will place the Heart and Square behind the Triangle, because they are children of BackLayer.

```
create sprite from triangle.png as Triangle
  where center=auto x=250 y=250
end
create sprite from heart.png as Heart
  where center=auto x=260 y=220
end
create sprite from square.png as Square
  where center=auto x=220 y=270
end
create sprite as BackLayer end
create routine as Start
  launch BackLayer
  launch Triangle
  set parent=BackLayer
  launch Heart
  launch Square
  set parent=_none
end
```
Set the parent to *_none* so that sprites added to the canvas afterwards resume the normal behavior of being drawn on top.

## Adding and Removing Children
You can add and remove child sprites from their parents in routines using **update** commands. Remove a child sprite does not delete it from canvas, it simply repositions it as if not attached to the sprite all.

In the example below, **click** the canvas to remove the sprite from the parent. You'll see how it move to position 100,100 on the canvas rather than the parent sprite.
```
create sprite from triangle.png as Triangle
  where center=auto x=250 y=250
  having children=Heart alt=(sub create rotation end)
end
create sprite from heart.png as Heart
  where center=auto x=100 y=100
end
create routine as Start
  launch Triangle
  set click=OnClick
end
create routine as OnClick
  remove from Triangle where children=Heart
end
```
If you want to remove a child sprite but have it maintain its current screen position and orientation, then use **detach**
```
create sprite from triangle.png as Triangle
  where center=auto x=250 y=250
  having children=Heart alt=(sub create rotation end)
end
create sprite from heart.png as Heart
  where center=auto x=100 y=100
end
create routine as Start
  launch Triangle
  set click=OnClick
end
create routine as OnClick
  remove from Triangle where detach=Heart
end
```

The compliment of removing and detaching are inserting and attaching. Those command look like this:

```
insert into Triangle where attach=Heart
insert into Triangle where children=Heart
```



# Sprites

Each moving entity is called a sprite. Normally sprites are defined with an image and a name. You can create a sprite without an image then use it as a container for other things. Sprites support many parameters. 
Sprites are moved using alterations. Alterations include rotation, slide and many more. 
The general animation structure of SCL is based on this relationship.

A sprite is put on screen with either the **launch** command or the **clone** command. Launch is the simplest and most direct method to do this and usually starts an animation in the **Start** routine.
After a sprite is on the screen it can be changed in a routine through various methods described later. 

If you want to change a sprite that you just created with a clone command, then refer to the clone by _clone instead of a sprite name. 

If you want to refer to a sprite that signalled an event (such as click or completion event), then use _sprite to refer to the current sprite. 

```
create routine as Start
  launch Dog
  launch windfarm
end
create sprite from dog.png as Dog
  where center=auto x=50 y=50
end
create sprite from windmill.png as windmill into windfarm 
  where x=100 y=200 alpha=0.5 angle=60 vflip=true xscale=2.5 yscale=5.6
    beenhit=OnBeenHit 
    center=20,25 click=clickWM composition="xor" 
    enterframe=clickWM exitframe=exitFrame
    hashit=OnHasHit hflip=true 
    mousein=mouseInWM mousemove=mouseMoveWM
    mouseout=mouseOutWM 
    parent=PlayArea penultimate=penultimateExit
  having target=bumbleBee children=(fin1,fin2) 
    alt=spinal,moveUp 
  set memvar1="dog" memvar2=4 
end
```

## Parameters
- **into** sprites can be grouped and the groups can be used as subjects or targets of different operations (ex: collision detection).
    - {spriteGroup} name a group into which to place this sprite
- **where** clause lists properties of the sprite. Here they are:
  - alpha={range: 0.0-1.0} with 1.0 being fully opaque
  - angle={number 0-360} angle to display the sprite relative to the unit circle (0 degrees is default and is pointing towards the right)
  - beenhit={routine} specify a routine to run when it has been hit by another sprite
  - center={number},{number}|{auto} where auto is the center of the image composing the sprite
  - click={routine} specify a routine to run when the mouse click while on an opaque portion of the sprite.
  - composition={"source-atop"| "source-in"| "source-out"| "source-over"| "destination-atop"| "destination-in"| "destination-out"| "destination-over"| "lighter"| "copy"| "xor"}
  - enterframe={routine} specify a routine to run when the frame is entere
  - exitframe={routine} specify a routine to run after the penultimate event and after all variable conditions have been checked and all the completion routines have finished running.
  - hashit={routine} specify a routine to run when this sprite has hit its target (see target below)
  - hflip={true|false} flip the sprite horizontally about the center point.
  - mousedown={routine} specify a routine to run when the mouse depressed over an opaque portion of the sprite
  - mouseup={routine} specify a routine to run when the mouse button is released over an opaque portion of the sprite
  - mousein={routine} specify a routine to run when the mouse moves onto an opaque portion of the sprite
  - mousemove={routine} specify a routine to run when the mouse moves within an opaque portion of the sprite
  - mouseout={routine} specify a routine to run when the mouse moves out of an opaque portion of the sprite
  - parent={sprite} specify a parent sprite onto which to attach this sprite as a child
  - penultimate={routine} specify a routine to run after all sprites are processed for the frame, but before the var conditions are tested
  - size={number} scale the sprite relative to 1
  - srcx={number} mark off a square region of the sprite to be displayed
  - srcy={number} mark off a square region of the sprite to be displayed
  - srcw={number} mark off a square region of the sprite to be displayed
  - srch={number} mark off a square region of the sprite to be displayed
  - swipedown={routine} specify a routine to run when the a downward swipe is performed over an opaque portion of the sprite
  - swipeleft={routine} specify a routine to run when the a leftward swipe is performed over an opaque portion of the sprite
  - swiperight={routine} specify a routine to run when the a rightward swipe is performed over an opaque portion of the sprite
  - swipeup={routine} specify a routine to run when the the a upward swipe is performed over an opaque portion of the sprite
  - touchdrag={routine} indicates that the sprite can be dragged via touch on a touch screen device
  - touchend={routine} specify a routine to run when a sprite touch is lifted off a touch screen device
  - touchstart={routine} specify a routine to run when the sprite is first touched via a touch screen device
  - vflip={true|false} vertical position of sprite based on the sprite's center
  - x={number}
  - xscale={number} scale the sprite horizontally
  - y={number}
  - yscale={number} scale the sprite vertically
 - **set** {name}={value} creates a variable specific only to the sprite. Do not use the same name as one of the built in properties
 - **having** clause lists objects that the sprite contains:
   - alt={alteration} alt={alteration}...
   - children=({sprite},{sprite},...) specify a list of sprites that move and orient themselves relative to this sprite. Children sprites do not need to be launched separately but are automatically launched when its parent sprite is launched. Note, child sprites absorb events and do not relay them to the parent. Set touch events on child sprites, not the parent sprite. This will change in upcoming releases.
   - target={sprite} specify a single sprite for which to trigger a collision event
   - targets={spriteGroup} specify a group of sprites which can all trigger collision events. Sprites are assigned to groups using "into" (see above).
  
## Groups, Targets, and Collision Detection
Groups, Targets, and hit detection all work together.

Collision detection is done using a sick and pathetic method devised to overcome browser deficiencies in place around 2010. I really need to update it, but for now, this is how it does *not* work: it does not use collision or bounding boxes.

Collision detection works by checking if the center point of a sprite occupies the same location as an opaque pixel of another sprite. I'm angry too, but it works good enough as you can see here [https://playscl.com/](https://playscl.com/g/invaders)https://playscl.com/g/invaders

Here is sample code:
```
create sprite from triangle.png as Triangle
  where x=100 y=450 center=auto hashit=OnHasHit
  having alt=SlideUp targets=MyHittableThings
end
create routine as OnHasHit
  update sprite _sprite where image=square.png
end
create slide as SlideUp
  where direction=90 speed=200 distance=500 completion=StopX
end
create routine as StopX
  Stop()
end
create sprite from star.png as Star into MyHittableThings
  where x=100 y=100 center=auto beenhit=OnBeenHit
end
create sprite from heart.png as Heart into MyHittableThings
  where x=100 y=300 center=auto beenhit=OnBeenHit
end
create routine as OnBeenHit
  insert into _sprite where alt=Rotation1
end
create rotation as Rotation1
  where speed=800 easein=100 easeout=100 distance=1440
end
create routine as Start
  launch Star
  launch Heart
  launch Triangle
end
```
In the example above, a triange moves upward. When it encounters strikes an object in the *MyHittableThings* group, it will run the **hashit** event handler called *OnHasHit*. This will replace the image used for the sprite to be a square.

The two target sprites, via the **beenhit** event handler, *OnBeenHit* will cause the objects to spin when hit by inserting a rotation alteration to themselves.

Finally when the triangle reaches the end of its journey **Stop()** will be called to end the animation. **Stop** cannot be specified as a completion event, therefore the *StopX* routine is used as a convention to actually call **Stop()**.

Also note that there are 2 similarly named values, **target** specifies a single sprite by name, and **targets** specifies a group name.


